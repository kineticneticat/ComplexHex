plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "me.modmuss50.mod-publish-plugin" version "0.3.0"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
    runs {
        client {
            programArgs(["--username", "kineticcat_", "--uuid", "23e372d5-6daa-4047-8f3a-c151fcc2799b"])
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modCompileOnly(group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2') // for @Nonnull

    // Loaders and base APIs
    modImplementation "net.fabricmc:fabric-loader:${project.fabricLoaderVersion}"
    modApi "net.fabricmc.fabric-api:fabric-api:${fabricApiVersion}"
    modImplementation("net.fabricmc:fabric-language-kotlin:${project.fabricKotlinVersion}")
    modApi("dev.architectury:architectury-fabric:${architecturyVersion}") {
        // Fix for the "two fabric loaders" loading crash
        exclude group: "net.fabricmc", module: "fabric-loader"
    }
    // Hex Casting stuff
    modImplementation("at.petra-k.hexcasting:hexcasting-fabric-${minecraftVersion}:${hexcastingVersion}") {
        // If not excluded here, calls a nonexistent method and crashes the dev client
        exclude module: "phosphor"
    }

    modImplementation "at.petra-k.paucal:paucal-fabric-${minecraftVersion}:${paucalVersion}"
    modImplementation "vazkii.patchouli:Patchouli:${minecraftVersion}-${patchouliVersion}-FABRIC"

    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-api:${cardinalComponentsVersion}"

    modImplementation "com.jamieswhiteshirt:reach-entity-attributes:${entityReachVersion}"
    include "com.jamieswhiteshirt:reach-entity-attributes:${entityReachVersion}"

    modImplementation files("${rootProject.rootDir}/lib/serialization-hooks-0.4.99999.jar")
    include files("${rootProject.rootDir}/lib/serialization-hooks-0.4.99999.jar")

    modImplementation files("${rootProject.rootDir}/lib/hexal-fabric-1.20.1-0.3.0-2.jar")
//    include files("${rootProject.rootDir}/lib/hexal-fabric-1.20.1-0.3.0-2.jar")

    modImplementation files("${rootProject.rootDir}/lib/moreiotas-fabric-1.20.1-0.1.0-4.jar")
//    include files("${rootProject.rootDir}/lib/moreiotas-fabric-1.20.1-0.1.0-4.jar")

    //modImplementation "dev.emi:trinkets:${trinketsVersion}"

    implementation group: "org.jblas", name: "jblas", version: "1.2.5"
    include group: "org.jblas", name: "jblas", version: "1.2.5"

    modApi("me.shedaniel.cloth:cloth-config-fabric:$clothConfigVersion") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
    from(rootProject.file("common/src/main/resources")) {
        include("data/*/patchouli_books/")
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set null
}

jar {
    archiveClassifier.set "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = project.archivesBaseName + "-" + project.name + "-fabric"
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

publishMods {
    file = remapJar.archiveFile
    additionalFiles.from(sourcesJar.archiveFile)
    changelog = getLatestChangelog()
    type = STABLE
    modLoaders.add("fabric")
    modLoaders.add("quilt")

    version = project.version + "-fabric"
    displayName = "Complex Hex $project.version [Fabric]"

    // Uncomment your desired platform(s)
//    curseforge {
//        accessToken = project.curseforgeApiToken
//        projectId = project.curseforgeId
//        minecraftVersions.add(minecraftVersion)
//
//        requires{
//            slug = "fabric-api"
//        }
//        requires {
//            slug = "architectury-api"
//        }
//        requires {
//            slug = "fabric-language-kotlin"
//        }
//        requires {
//            slug = "hexcasting"
//        }
//    }
//
//    modrinth {
//        accessToken = project.modrinthApiToken
//        projectId = project.modrinthId
//        minecraftVersions.add("1.19.2")
//
//        requires{
//            slug = "fabric-api"
//        }
//        requires {
//            slug = "architectury-api"
//        }
//        requires {
//            slug = "fabric-language-kotlin"
//        }
//        requires {
//            slug = "hex-casting"
//        }
//    }
}
